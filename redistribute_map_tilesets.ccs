// Redistribute Map Tilesets v1.0
// Published at https://gist.github.com/esorth/7443d116d1c56b4acc2977329337eedf
// Developed at https://github.com/esorth/EbCcscripts/blob/main/redistribute_map_tilesets.ccs
//
// Copyright 2024 Eric Orth
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the “Software”), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//

// Explanation:
//
// An EarthBound tileset contains a list of 896 characters (often called
// "minitiles"). Normally the first 512 are background characters, and the
// remaining 384 are foreground characters. The first 384 background characters
// are automatically tied to and rendered with foreground characters (character
// 0 tied to character 512, character 1 tied to character 513, etc). And the
// remaining 128 background characters are always rendered on their own. In
// EbProjEdit, the characters are always shown as 512 entries, the first 384 of
// which are doubled up to allow modifying the separate background and
// foreground characters.
//
// This script moves the border between background and foreground via setting a
// per-tileset configuration in the TilesetRedistributionTable below. So with a
// tileset adjusted to 536 background characters, they would now be followed by
// 360 foreground characters, and the pairing would be character 0 tied to
// character 536, character 1 tied to character 537, etc. EbProjEdit doesn't
// know what to do with it and the TileEditor pairs up characters that won't
// actually be paired up when rendered in EarthBound.
//
// Additionally, this script allows map tile entries to directly specify
// foreground characters, which in some rare cases may be used to further
// stretch the graphical flexibility of a tileset. E.g., if a tileset is
// configured for the standard 512 background characters, a map tile could
// specify character 600, a foreground character. EarthBound would then render
// just the foreground character without pairing with any background character,
// which would typically look weird unless the foreground character fills the
// entire 8x8 space.


import asm65816

// Table declaring the number of background characters for each tileset. Each
// tileset must have at most 896 background characters. Table is indexed by
// tileset index
// (https://datacrystal.tcrf.net/wiki/EarthBound/Map_Data/Tileset_Table#Values).
TilesetRedistributionTableVersion:
  byte 01
TilesetRedistributionTableCount:
  byte 20
TilesetRedistributionTable:
  short 512  // 00: Lost underworld
  short 512  // 01: Onett
  short 512  // 02: Twoson
  short 512  // 03: Threed
  short 512  // 04: Fourside
  short 512  // 05: Magicant
  short 512  // 06: Outdoors
  short 512  // 07: Summers
  short 512  // 08: Dusty Dunes Desert
  short 512  // 09: Dalaam
  short 512  // 10: Indoors 1
  short 512  // 11: Indoors 2
  short 512  // 12: Stores 1
  short 512  // 13: Caves 1
  short 512  // 14: Indoors 3
  short 512  // 15: Stores 2
  short 512  // 16: Indoors 4
  short 512  // 17: Winters
  short 512  // 18: Scaraba
  short 512  // 19: Caves 2
  
// Set overworld map BG2 to start at VRAM word 0 (same as BG1). This results in
// both backgrounds sharing the same pool of 896 characters.
ROM[0xC0002A] = short 0
ROM[0xC01952] = short 0

// Replace 0xC00ED1-0xC00EE5 in LoadTilemapRowToVram()
ROM[0xC00ED1] = {
  PHX
  JSL(LoadForegroundTile)
  PLX
  JMP(0x0EE6)
}

// Replace 0xC01081-0xC01095 in LoadTilemapColumnToVram()
ROM[0xC01081] = {
  PHX
  JSL(LoadForegroundTile)
  PLX
  JMP(0x1096)
}

// Mutates $12, initially containing a map background tilemap entry, into a
// map foreground tilemap entry
LoadForegroundTile:
  // 6 bytes of DP space
  PHD
  TDC
  ADC_i(0xfffa)
  TCD
  
  // Get table size
  // $00 = TilesetRedistributionTableCount & #$00ff
  LDA_al(TilesetRedistributionTableCount)
  AND_i(0x00ff)
  STA_d(0x00)
  
  // Validate table size
  // if (current_tileset_index >= $00) break
  LDA_al(0x7e4372)
  CMP_d(0x00)
  BCC(1)
  BRK

  // Calculate table index
  // x = current_tileset_index * 2
  ASL
  TAX

  // Get num background tiles
  // $02 = TilesetRedistributionTable,x
  LDA_xl(TilesetRedistributionTable)  // Background size
  STA_d(0x02)
  
  // if ($02 >= #897) break
  CMP_i(896)
  BCC(1)
  BRK
  
  // Determine num foreground tiles
  // $04 = #896 - $02
  LDA_i(896)
  SEC
  SBC_d(0x02)
  STA_d(0x04)
  
  // If tile is already a foreground tile, use it directly as such
  // if (($18 & #$03ff) >= $02)
  LDA_d(0x18)
  AND_i(0x03ff)
  CMP_d(0x02)
  BCC_a(else0)
  
  // $18 |= #$2000
  LDA_d(0x18)
  ORA_i(0x2000)
  STA_d(0x18)
  BRA_a(end0)
  
else0:
  // If tile has linked foreground tile, convert to foreground tilemap entry
  // if (($18 & #$03ff) < $04)
  CMP_d(0x04)
  BCS_a(else1)

  // $18 |= #$2000
  // $18 += $02
  LDA_d(0x18)
  ORA_i(0x2000)
  CLC
  ADC_d(0x02)
  STA_d(0x18)
  BRA_a(end0)

else1:
  // $18 = #0
  STZ_d(0x18)

end0:

  PLD
  RTL
